plugins {
  id 'java-library'
  id 'application'
  alias(libs.plugins.download)
  alias(libs.plugins.graalvmNativeImage)
  alias(libs.plugins.versions)
  alias(libs.plugins.errorprone)
  alias(libs.plugins.shadowJar)
}

def system = org.gradle.internal.os.OperatingSystem.current()
def arch = System.getProperty("os.arch")
if (system.isLinux() && arch == "amd64") {
  project.ext.osArch = "x64"
  project.ext.opSystem = "linux"
  project.ext.graalExt = "tar.gz"
  project.ext.graalHome = "graalvm"
  project.ext.upxArch = "amd64_linux"
  project.ext.upxExt = "tar.xz"
} else if (system.isLinux() && arch == "aarch64") {
  project.ext.osArch = "aarch64"
  project.ext.opSystem = "linux"
  project.ext.graalExt = "tar.gz"
  project.ext.graalHome = "graalvm"
  project.ext.upxArch = "arm64_linux"
  project.ext.upxExt = "tar.xz"
} else if (system.isMacOsX() && arch == "x86_64") {
  project.ext.osArch = "x64"
  project.ext.opSystem = "macos"
  project.ext.graalExt = "tar.gz"
  project.ext.graalHome = "graalvm/Contents/Home"
  project.ext.upxArch = "amd64_darwin" // not supported :(
  project.ext.upxExt = "tar.xz"
} else if (system.isMacOsX() && arch == "aarch64") {
  project.ext.osArch = "aarch64"
  project.ext.opSystem = "macos"
  project.ext.graalExt = "tar.gz"
  project.ext.graalHome = "graalvm/Contents/Home"
  project.ext.upxArch = "arm64_darwin" // not supported :(
  project.ext.upxExt = "tar.xz"
} else if (system.isWindows()) {
  project.ext.osArch = "x64"
  project.ext.opSystem = "windows"
  project.ext.graalExt = "zip"
  project.ext.graalHome = "graalvm"
  project.ext.upxArch = "win64"
  project.ext.upxExt = "zip"
}

group = 'com.github.tonivade'

base {
  archivesName = 'pq'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(23)
  }
}

test {
  useJUnitPlatform()

  reports {
    html.required = true
  }
}

javadoc.options {
  addStringOption('-release', '23')
}

compileJava {
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xlint:rawtypes'
  options.release = 23
}

compileTestJava {
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xlint:rawtypes'
  options.release = 23
}

tasks.withType(JavaCompile) {
  if (!name.toLowerCase().contains("test")) {
    options.errorprone {
      check("NullAway", net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
      option("NullAway:AnnotatedPackages", "pq")
      option("NullAway:SuggestSuppressions", "true")
      option("NullAway:KnownInitializers", "pq.internal.JsonArrayHolder.create,pq.internal.JsonObjectHolder.create,org.apache.parquet.hadoop.api.WriteSupport.prepareForWrite")
    }
  } else {
    options.errorprone.enabled = false
  }
}

dependencies {
  errorprone(libs.nullaway)
  errorprone(libs.errorprone)
  compileOnly(libs.jsr305)
  implementation(libs.picocli)
  implementation(libs.parquetColumn)
  implementation(libs.parquetHadoop)
  implementation(libs.petitparser)
  implementation(libs.minimalJson)
  // force version of snappy
  implementation(libs.snappy)
  implementation(libs.hadoopMapreduceClient) {
    exclude group: 'org.apache.avro', module: 'avro'
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    exclude group: 'org.apache.hadoop', module: 'hadoop-yarn-client'
    exclude group: 'org.apache.hadoop', module: 'hadoop-yarn-common'
    exclude group: 'org.apache.hadoop', module: 'hadoop-hdfs-client'
    exclude group: 'com.google.inject.extensions', module: 'guice-servlet'
    exclude group: 'io.netty', module: 'netty-all'
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
    exclude group: 'javax.ws.rs', module: 'javax.ws.rs-api'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'org.slf4j', module: 'slf4j-reload4j'
    exclude group: 'com.sun.jersey', module: 'jersey-json'
    exclude group: 'com.github.pjfanning', module: 'jersey-json'
  }
  implementation(libs.hadoopCommon) {
    exclude group: 'org.apache.avro', module: 'avro'
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    exclude group: 'org.apache.kerby', module: 'kerb-core'
    exclude group: 'org.apache.curator', module: 'curator-recipes'
    exclude group: 'org.apache.curator', module: 'curator-client'
    exclude group: 'org.apache.hadoop', module: 'hadoop-annotations'
    exclude group: 'org.apache.hadoop', module: 'hadoop-auth'
    exclude group: 'org.apache.hadoop.thirdparty', module: 'hadoop-shaded-protobuf_3_7'
    exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    exclude group: 'org.apache.commons', module: 'commons-text'
    exclude group: 'org.apache.commons', module: 'commons-math3'
    exclude group: 'org.apache.commons', module: 'commons-lang3'
    exclude group: 'org.apache.commons', module: 'commons-configuration2'
    exclude group: 'org.apache.zookeeper', module: 'zookeeper'
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
    exclude group: 'org.eclipse.jetty', module: 'jetty-servlet'
    exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
    exclude group: 'org.eclipse.jetty', module: 'jetty-util'
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
    exclude group: 'javax.servlet.jsp', module: 'jsp-api'
    exclude group: 'com.sun.jersey', module: 'jersey-json'
    exclude group: 'com.sun.jersey', module: 'jersey-core'
    exclude group: 'com.sun.jersey', module: 'jersey-servlet'
    exclude group: 'com.sun.jersey', module: 'jersey-server'
    exclude group: 'com.google.guava', module: 'guava'
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
    exclude group: 'commons-beanutils', module: 'commons-beanutils'
    exclude group: 'commons-codec', module: 'commons-codec'
    exclude group: 'commons-io', module: 'commons-io'
    exclude group: 'commons-net', module: 'commons-net'
    exclude group: 'commons-cli', module: 'commons-cli'
    exclude group: 'commons-pool', module: 'commons-pool'
    exclude group: 'ch.qos.reload4j', module: 'reload4j'
    exclude group: 'org.slf4j', module: 'slf4j-reload4j'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'log4j', module: 'log4j'
    exclude group: 'com.jcraft', module: 'jsch'
    exclude group: 'dnsjava', module: 'dnsjava'
    exclude group: 'com.google.code.gson', module: 'gson'
    exclude group: 'org.apache.kerby', module: 'kerb-simplekdc'
    exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
    exclude group: 'net.minidev', module: 'json-smart'
    exclude group: 'org.apache.curator', module: 'curator-framework'
    exclude group: 'jakarta.activation', module: 'jakarta.activation-api'
    exclude group: 'io.netty', module: 'netty-handler'
    exclude group: 'io.netty', module: 'netty-transport-native-epoll'
    exclude group: 'com.github.pjfanning', module: 'jersey-json'
    exclude group: 'org.codehaus.jettison', module: 'jettison'
    exclude group: 'com.google.re2j', module: 're2j'
    exclude group: 'io.dropwizard.metrics', module: 'metrics-core'
    exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
  }
  implementation(libs.logbackClassic)
  implementation(libs.slf4j)

  testImplementation(platform(libs.junitBom))
  testImplementation(libs.junitJupiter)
  testRuntimeOnly(libs.junitLauncher)
  testImplementation(libs.mockitoCore)
  testImplementation(libs.mockitoJunit)
  testImplementation(libs.assertj)
  testImplementation(libs.systemStubs)
}

application {
  mainClass = 'pq.App'
}

shadowJar {
  manifest {
    attributes "Main-Class": "pq.App"
  }
}

task downloadGraalvm(type: Download) {
  src "https://github.com/graalvm/graalvm-ce-builds/releases/download/jdk-${graalvm_version}/graalvm-community-jdk-${graalvm_version}_${opSystem}-${osArch}_bin.${graalExt}"
  dest new java.io.File(buildDir, "graalvm.tar.gz")
  onlyIfModified true
}

task extractGraalvm(dependsOn: downloadGraalvm, type: Exec) {
  workingDir "$buildDir"
  if (system.isWindows()) {
    commandLine 'cmd', '/c', 'if not exist graalvm (mkdir graalvm) else (cd.) && tar -xvf graalvm.tar.gz -C graalvm --strip-components 1'
  } else {
    commandLine 'bash', '-c', 'mkdir -p graalvm && tar -xvf graalvm.tar.gz -C graalvm --strip-components 1'
  }
}

nativeImage {
  graalVmHome = "${buildDir}/${graalHome}"
  mainClass = "pq.App"
  executableName = "pq"
  arguments(
          '-J-Xms2G',
          '-J-Xmx2G',
          '--verbose',
          '--report-unsupported-elements-at-runtime',
          '--no-fallback',
          '--allow-incomplete-classpath',
          '--initialize-at-build-time=ch.qos.logback',
          '--initialize-at-build-time=org.slf4j.LoggerFactory',
          '--initialize-at-build-time=org.slf4j.helpers.SubstituteServiceProvider',
          '--initialize-at-build-time=org.slf4j.helpers.NOP_FallbackServiceProvider',
          '--initialize-at-build-time=org.slf4j.helpers.SubstituteLoggerFactory',
          '--initialize-at-build-time=org.slf4j.helpers.NOPLoggerFactory',
          '--initialize-at-build-time=org.slf4j.helpers.BasicMDCAdapter',
          '--initialize-at-build-time=org.slf4j.helpers.NOPMDCAdapter',
          '-H:IncludeResources=logback.xml'
  )
}

task downloadUpx(type: Download) {
  src "https://github.com/upx/upx/releases/download/v${upx_version}/upx-${upx_version}-${upxArch}.${upxExt}"
  dest new java.io.File(buildDir, "upx.tar.xz")
  onlyIfModified true
}

task extractUpx(dependsOn: downloadUpx, type: Exec) {
  workingDir "$buildDir"
  if (system.isWindows()) {
    commandLine 'cmd', '/c', 'if not exist upx (mkdir upx) else (cd.) && tar -xvf upx.tar.xz -C upx --strip-components 1'
  } else {
    commandLine 'bash', '-c', 'mkdir -p upx && tar -xvf upx.tar.xz -C upx --strip-components 1'
  }
}

task compressExecutable(dependsOn: extractUpx, type: Exec) {
  workingDir "$buildDir/upx"
  if (system.isWindows()) {
    commandLine 'cmd', '/c', 'upx --best ..\\native-image\\pq.exe'
  } else {
    commandLine 'bash', '-c', './upx --best ../native-image/pq'
  }
}

task testNativeImage(type: Exec) {
  workingDir "$buildDir/native-image"
  if (system.isWindows()) {
    commandLine 'cmd', '/c', 'pq.exe count ..\\..\\src\\test\\resources\\example.parquet'
  } else {
    commandLine 'bash', '-c', './pq count ../../src/test/resources/example.parquet'
  }
}

installNativeImage {
  dependsOn extractGraalvm
}
